Package sc;

Helpers

lettre = [['a' .. 'z'] + ['A' .. 'Z']];
chiffre = ['0' .. '9'];

Tokens

entier = 'entier';
lire = 'lire';
ecrire = 'ecrire';
ou = '|';
et = '&';
non = '!';
egale = '=';
inf = '<';
plus = '+';
moins = '-';
mult = '*';
div = '/';
po = '(';
pf = ')';
ao = '{';
af = '}';
co = '[';
cf = ']';
virg = ',';
pv = ';';

si = 'si';
sinon = 'sinon';
alors = 'alors';
tantque = 'tantque';
faire = 'faire';
retour = 'retour';

espaces = (' ' | 13 | 10)+;
commentaire = '#' [[0 .. 0xffff] - [10 + 13]]* (10 | 13 | 10 13);


const = chiffre+;
ident = lettre (lettre | chiffre)*;



Ignored Tokens
espaces, commentaire;




Productions

///////////////////////////////////////////////////////////////////////////////////////////
// A

program =
    {with_var} dec_l_var pv dec_l_func |
    {no_var} dec_l_func;


///////////////////////////////////////////////////////////////////////////////////////////
// B

dec_l_var =
    {var} dec_var dec_l_var_b |
    {no_var} ;

dec_l_var_b =
    {var} virg dec_var dec_l_var_b |
    {end} ;

dec_var =
    {entier} entier ident |
    {tab} entier ident co const cf;



///////////////////////////////////////////////////////////////////////////////////////////
// C

dec_l_func =
    {func} dec_func dec_l_func |
    {no_func} ;

dec_func =
    {with_var} ident po [args]: dec_l_var pf [vars]: dec_l_var pv bloc |
    {no_var} ident po dec_l_var pf bloc;


///////////////////////////////////////////////////////////////////////////////////////////
// D

bloc = ao l_inst af;


///////////////////////////////////////////////////////////////////////////////////////////

l_inst =
    {inst} inst l_inst |
    {no_inst} ;

inst =
    {bloc} bloc |
    {si} i_si |
    {tantque} i_tantque |
    {aff} i_aff |
    {retour} i_retour |
    {func} i_func |
    {ecrire} i_ecrire;


///////////////////////////////////////////////////////////////////////////////////////////


i_aff = var egale exp pv;

i_si =
    {no_sinon} si exp alors bloc |
    {with_sinon} si exp alors [si_bloc]: bloc sinon [sinon_bloc]: bloc;

i_tantque = tantque exp faire bloc;

i_retour = retour exp pv;

i_func =
    {app_func} func pv |
    {app_lire} lire po pf pv;

i_ecrire = ecrire po exp pf pv;


///////////////////////////////////////////////////////////////////////////////////////////


var =
    {entier} ident |
    {tab_entier} ident co exp cf;

func = ident po func_l_arg pf;

func_l_arg =
    {arg} exp arg |
    {no_arg} ;

arg =
    {exp} virg exp arg |
    {no_exp} ;


///////////////////////////////////////////////////////////////////////////////////////////


exp =
    {ou} exp ou exp1 |
    {to_exp1} exp1;

exp1 =
    {et} exp1 et exp2 |
    {to_exp2} exp2;

exp2 =
    {egale} exp2 egale exp3 |
    {inf} exp2 inf exp3 |
    {to_exp3} exp3;

exp3 =
    {plus} exp3 plus exp4 |
    {moins} exp3 moins exp4 |
    {to_exp4} exp4;

exp4 =
    {mult} exp4 mult exp5 |
    {div} exp4 div exp5 |
    {to_exp5} exp5;

exp5 =
    {non} non exp5 |
    {to_exp6} exp6;

exp6 =
    {exp_par} po exp pf |
    {const} const |
    {func} func |
    {var} var |
    {lire} lire po pf;
